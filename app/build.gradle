apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        release {
            try {
                storeFile file('../keystore.jks')
                storePassword KEYSTORE_PASSWORD
                keyAlias 'DeploymentKey'
                keyPassword KEY_PASSWORD
            } catch (ignored) {
                throw new InvalidUserDataException("You should define KEYSTORE_PASSWORD and KEY_PASSWORD in gradle.properties.")
            }
        }
    }

    defaultConfig {
        applicationId "com.stumi.gobuddies"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        prod {
            resValue "string", "app_name", "GO Buddies"
        }
        develop {
            versionName "1.0-develop"
            resValue "string", "app_name", "GO Buddies-DEV"
            applicationId "com.stumi.gobuddies.develop"
        }
    }

    sourceSets {
        main {

            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java', '.apt_generated']
            aidl.srcDirs = ['src/main/aidl', '.apt_generated']
            assets.srcDirs = ['src/main/assets']

            res.srcDirs = [
                    'src/main/res/bubble',
                    'src/main/res/partyFinder',
                    'src/main/res/party',
                    'src/main/res',
            ]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    //compile project(':rxlocation')

    //android
    compile 'com.android.support:appcompat-v7:' + rootProject.ext.androidVersion
    compile 'com.android.support:cardview-v7:' + rootProject.ext.androidVersion
    compile 'com.android.support:recyclerview-v7:' + rootProject.ext.androidVersion
    compile 'com.android.support:design:' + rootProject.ext.androidVersion
    compile 'com.google.android.gms:play-services-maps:' + rootProject.ext.playServicesVersion
    //compile 'com.google.android.gms:play-services-appindexing:' + rootProject.ext.playServicesVersion

    compile 'org.glassfish:javax.annotation:10.0-b28'

    //3rd party
    //pattern
    compile 'com.hannesdorfmann.mosby:mvp:2.0.1'
    compile 'org.greenrobot:eventbus:3.0.0'
    compile 'com.annimon:stream:1.1.3'
    compile 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:1.0.1'

    //RX
    compile 'io.reactivex.rxjava2:rxandroid:' + rootProject.ext.rxandroidVersion
    compile 'io.reactivex.rxjava2:rxjava:' + rootProject.ext.rxjavaVersion
    compile 'com.jakewharton.rxbinding2:rxbinding:' + rootProject.ext.rxBindingVersion
    compile 'com.jakewharton.rxbinding2:rxbinding-support-v4:' + rootProject.ext.rxBindingVersion
    compile 'com.jakewharton.rxbinding2:rxbinding-appcompat-v7:' + rootProject.ext.rxBindingVersion
    compile 'com.jakewharton.rxbinding2:rxbinding-design:' + rootProject.ext.rxBindingVersion
    compile 'com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:' + rootProject.ext.rxBindingVersion
    compile 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    //UI
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.easing:library:1.0.2@aar'
    compile 'com.daimajia.androidanimations:library:1.1.3@aar'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile('com.github.ganfra:material-spinner:1.1.1') {
        // exclude because of thr materialedittext
        exclude group: 'com.nineoldandroids', module: 'library'
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    apt 'com.hannesdorfmann.annotatedadapter:processor:' + rootProject.ext.annotatedadapterVersion
    compile 'com.hannesdorfmann.annotatedadapter:annotation:' + rootProject.ext.annotatedadapterVersion
    compile 'com.hannesdorfmann.annotatedadapter:support-recyclerview:' + rootProject.ext.annotatedadapterVersion
    compile 'com.andkulikov:transitionseverywhere:1.7.0'
    compile 'com.github.dmytrodanylyk:android-morphing-button:98a4986e56' // commit hash
    compile 'org.osmdroid:osmdroid-android:5.6.4'
    compile 'net.steamcrafted:load-toast:1.0.10'
    compile 'com.github.GrenderG:Toasty:1.1.4'

    //DI
    compile 'com.google.dagger:dagger:' + rootProject.ext.daggerVersion
    apt "com.google.dagger:dagger-compiler:" + rootProject.ext.daggerVersion
    compile 'com.jakewharton:butterknife:' + rootProject.ext.butterknifeVersion
    apt 'com.jakewharton:butterknife-compiler:' + rootProject.ext.butterknifeVersion

    //network
    compile 'com.squareup.retrofit2:retrofit:' + rootProject.ext.retrofitVersion
    compile 'com.squareup.retrofit2:converter-gson:' + rootProject.ext.retrofitVersion
    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'

    //Android helper
    compile 'frankiesardo:icepick:' + rootProject.ext.icepickVersion
    provided 'frankiesardo:icepick-processor:' + rootProject.ext.icepickVersion
    compile 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.1@aar'

    compile 'com.orhanobut:logger:1.15'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5'

    //test
    testCompile 'junit:junit:4.12'
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.mockito:mockito-core:2.2.27"
    testCompile 'info.solidsoft.mockito:mockito-java8:2.0.0'

    //fabric
    compile('com.crashlytics.sdk.android:crashlytics:2.6.7@aar') {
        transitive = true;
    }

    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    }
}

